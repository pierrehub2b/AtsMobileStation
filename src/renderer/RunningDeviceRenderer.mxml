<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				autoDrawBackground="true" height="40"
				removedFromStage="removedFromStageHandler(event)"
				toolTip="{dev.tooltip}">
	
	<fx:Script>
		<![CDATA[
			import device.Device;
			import device.IosDevice;
			
			import flash.filters.BitmapFilterQuality;
			
			import mx.core.DragSource;
			import mx.managers.DragManager;
			import mx.managers.PopUpManager;
			
			
			import spark.filters.DropShadowFilter;
			
			private function showWindow(event:MouseEvent, dev:Device):void{
				// create and configure the TitleWindow
				var tw:DeviceSettingsWindow = new DeviceSettingsWindow();
				tw.dev = dev;
				// open the TitleWindow as a modal popup window
				PopUpManager.addPopUp(tw, this, true);
				PopUpManager.centerPopUp(tw);
			}
			
			private function goHome(event:MouseEvent, dev:Device):void{
				var request:URLRequest = new URLRequest();
				var urlLoader:URLLoader = new URLLoader();
				
				request.url = "http://" + dev.ip + ":" + dev.port + "/button";
				request.contentType = "text/plain";
				request.method = URLRequestMethod.POST;
				request.data = "home";
				
				urlLoader = new URLLoader();
				urlLoader.dataFormat = URLLoaderDataFormat.TEXT;
				urlLoader.load(request);
			}
			
			public static const dropShadowFilterMiddle:DropShadowFilter = new DropShadowFilter(4.0, 45, 0x000000, 0.2, 8.0, 8.0, 0.9, BitmapFilterQuality.HIGH, false, false, false);
			
			[Bindable]
			private var dev:Device;
			
			override public function set data(value:Object):void
			{
				super.data = value;
				this.dev = value as Device;
				
				if(this.dev is IosDevice){
					mobile.source = "assets/icons/32/ios.png";
				}else{
					mobile.source = "assets/icons/32/android.png";
				}
			}
			
			protected function restart_clickHandler(event:MouseEvent):void
			{
				AtsMobileStation.devices.restartDev(dev);
			}
			
			protected function mainGroup_rollOverHandler(event:MouseEvent):void
			{
				if(!DragManager.isDragging && (dev.status == "ready" || dev.status == "fail")){
					restartButton.includeInLayout = true;
					restartButton.visible = true;
					iconStatus.visible = false;
					iconStatus.includeInLayout = false;
				}
				
				if(this.dev is IosDevice) {
					settingsButton.includeInLayout = true;
					settingsButton.visible = true;
					homeButton.includeInLayout = true;
					homeButton.visible = true;
				}
			}
			
			protected function mainGroup_rollOutHandler(event:MouseEvent):void
			{
				if(this.dev is IosDevice) {
					settingsButton.includeInLayout = false;
					settingsButton.visible = false;
					homeButton.includeInLayout = false;
					homeButton.visible = false;
				}
				restartButton.includeInLayout = false;
				restartButton.visible = false;
				iconStatus.visible = true;
				iconStatus.includeInLayout = true;
			}
			
			protected function removedFromStageHandler(event:Event):void
			{
				mainGroup.filters = []
			}
			
			override protected function get hovered():Boolean { return false; }
			//override protected function get down():Boolean { return false; }
			override public function get selected():Boolean { return false; }
			//override public function get showsCaret():Boolean { return false; }
			
			protected function mouseDownHandler(event:MouseEvent):void
			{
				if(dev.status == "ready"){
					firstGroup.addEventListener(MouseEvent.MOUSE_MOVE, mouseDownForDragHandler, false, 0, true);
				}
			}
			
			protected function mouseDownForDragHandler(event:MouseEvent):void{
				firstGroup.removeEventListener(MouseEvent.MOUSE_MOVE, mouseDownForDragHandler);
				
				var dragSource:DragSource = new DragSource();
				dragSource.addData("", "mobile");
				
				Clipboard.generalClipboard.setData(ClipboardFormats.TEXT_FORMAT, dev.ip + ":" + dev.port, false);
				
				DragManager.doDrag(this, dragSource, event, firstGroup, mouseX - 20, mouseY + 10, 1.0);
			}
				
			
		]]>
	</fx:Script>
	
	<s:HGroup id="mainGroup" width="100%" verticalAlign="middle" verticalCenter="0" filters="{dropShadowFilterMiddle}"
			  paddingRight="6" paddingLeft="2" rollOver="mainGroup_rollOverHandler(event)" rollOut="mainGroup_rollOutHandler(event)">
		<s:HGroup id="firstGroup" verticalAlign="middle" mouseDown="mouseDownHandler(event)" buttonMode="true">
			<s:BitmapImage  id="mobile" source="assets/icons/32/android.png" smooth="true" smoothingQuality="high"/>
			<s:VGroup gap="3" mouseEnabled="false" verticalAlign="middle">
				<s:Label visible="{dev.ip==null}" includeInLayout="{dev.ip==null}" 
						 text="Unable to get local ip !" fontStyle="italic" color="0x8a844a"/>
				<s:HGroup visible="{dev.ip!=null}" includeInLayout="{dev.ip!=null}" gap="0">
					<s:Label text="{dev.ip}" fontWeight="bold"/>
					<s:Label text=":{dev.port}"/>
				</s:HGroup>
				<s:Label text="{dev.manufacturer} - {dev.modelName}" fontSize="11" fontStyle="italic" color="#758696"/>
			</s:VGroup>
		</s:HGroup>
		
		<s:Spacer width="100%"/>
		
		<s:Group id="homeButton" buttonMode="true" click="goHome(event, dev)"
				 toolTip="Device home button" visible="false" includeInLayout="false">
			<s:BitmapImage source="assets/icons/24/home.png" width="20" height="20" smooth="true" smoothingQuality="high"/>
		</s:Group>
		
		<s:Group id="settingsButton" buttonMode="true" click="showWindow(event, dev)"
				 toolTip="Settings of device" visible="false" includeInLayout="false">
			<s:BitmapImage source="assets/icons/24/run.png" smooth="true" smoothingQuality="high"/>
		</s:Group>
		
		<s:BitmapImage id="iconStatus" source="assets/icons/24/{dev.status}.png" smooth="true" smoothingQuality="high"/>
		<s:Group id="restartButton" buttonMode="true" click="restart_clickHandler(event)"
				 toolTip="Restart device" visible="false" includeInLayout="false">
			<s:BitmapImage source="assets/icons/24/restart.png" smooth="true" smoothingQuality="high"/>
		</s:Group>
	</s:HGroup>

</s:ItemRenderer>
